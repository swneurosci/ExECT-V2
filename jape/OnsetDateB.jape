Phase: OnsetDateB
Input: Lookup3 Person Lookup2 PointInTime Lookup DateSince Split
Options: control=appelt

/*For when the seizure type is specified in the sentence before the date of onset*/

Rule: OnsetDateB1
Priority: 100

(   
	{Person}
	({Lookup.majorType == umls , Lookup.Negation == Affirmed, Lookup.minorType == Disease , Lookup.Experiencer == Patient}):item
    {Split}
    ({Split})?
    {Person}
    {Lookup2.language == onset , Lookup2.type == began}
    {Lookup3.minorType == generic}
	{DateSince}
		
	
 ):match  
 -->
:item.Onset = {rule = OnsetDateB1, OnsetType = "date_of_onset", 
CUI = :item.Lookup.CUI,
PREF = :item.Lookup.PREF, TUI = :item.Lookup.TUI,
// STY = :item.Lookup.STY,  Negation = :item.Lookup.Negation, Experiencer = :item.Lookup.Experiencer,
// Certainty = :item.Lookup.Certainty,
YearDate = :match.DateSince.YearDate,
MonthDate =:match.DateSince.MonthDate,
DayDate = :match.DateSince.DayDate}

/* concept of interest is in a previous statement: ...for focal seizures. She started
 having these episodes in September 2010 - so it links focal seizures to 'these episodes' and the date*/
Rule: OnsetDateB2
Priority: 100

(   
	
	({Lookup.majorType == umls , Lookup.Negation == Affirmed, Lookup.minorType == Disease , Lookup.Experiencer == Patient}):item
    {Split}
    ({Split})?
    ({Split})?
    {Person}
    {Lookup2.language == onset , Lookup2.type == began}
    ({Lookup3.minorType == generic})?
	{DateSince}
		
 ):match  
 -->
:item.Onset = {rule = OnsetDateB2, OnsetType = "date_of_onset", 
CUI = :item.Lookup.CUI,
PREF = :item.Lookup.PREF, TUI = :item.Lookup.TUI,
// STY = :item.Lookup.STY,  Negation = :item.Lookup.Negation, Experiencer = :item.Lookup.Experiencer,
Certainty = :item.Lookup.Certainty,
YearDate = :match.DateSince.YearDate,
MonthDate =:match.DateSince.MonthDate,
DayDate = :match.DateSince.DayDate}


/*  As in: ...GTCS. The first one was in 2013...*/
Rule: OnsetDateB4  
Priority: 100

(   
	// Lookcup from epilepsy.lst or seizure.lst, which is affirmed (not negated) and refers to a Patient (not Other)
	({Lookup.majorType == umls , Lookup.Negation == Affirmed, Lookup.minorType == Disease , Lookup.Experiencer == Patient}):item
    {Split} //end of sentence
    {Lookup2.language == onset , Lookup2.type == began} //onset term based on Onset.lst
	{Lookup2} //any Lookup2 
	{DateSince} //from a rule defining date since = term (since, in) + DayDate
		
 ):match  
 --> // this is annotation output: Lookup with features as an Onset entity
:item.Onset = {rule = OnsetDateB4, OnsetType = "date_of_onset", 
CUI = :item.Lookup.CUI,
PREF = :item.Lookup.PREF, TUI = :item.Lookup.TUI,
// STY = :item.Lookup.STY,  Negation = :item.Lookup.Negation, Experiencer = :item.Lookup.Experiencer,
Certainty = :item.Lookup.Certainty, // certainty level transferred from the lookup
YearDate = :match.DateSince.YearDate, //date split into 3 elements, YearDate, MonthDate, DayDate
MonthDate =:match.DateSince.MonthDate,
DayDate = :match.DateSince.DayDate}
