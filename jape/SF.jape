Phase: SeizureFrequency
Input: Lookup3 Split SeizFreq SeizOnset Prescription0 SeizureFree ContextPrescription timePeriods NumberRange Age
Options: control=appelt

/*
 * Various rules for different ways of expressing a seizure frequency
 * i.e. "she has 4-5 seizures per day" or "he has 10 focal seizures weekly"
 * timePeriods.period only exits in some of the items in timePeriods gazetteer
*/



/*
* 3 seizures in the last month
*/

Rule: hSeizureFreq1a
Priority: 100
(
    ({ContextPrescription})?
    ({NumberRange})
    ({Lookup3.majorType == "Lookup", Lookup3.minorType == "Disease", Lookup3.Negation == Affirmed, Lookup3.Experiencer == Patient})
    ({timePeriods})
):match
-->
:match.SeizureFrequency1 = { rule = "hSeizure1a", CUI = :match.Lookup3.CUI, NoOfTimePeriods = :match.timePeriods.frequency, TimePeriod = :match.timePeriods.time-unit, QuantityTotal = :match.NumberRange.value}

/*
* 3 'episodes' in the last month
*/


Rule: hSeizureFreq1b
Priority: 99
(
    ({ContextPrescription})?
    ({NumberRange})
    ({Lookup3.majorType == "epilepsy", Lookup3.minorType == "seizure", Lookup3.Negation == Affirmed, Lookup3.Experiencer == Patient})
    ({timePeriods})
):match
-->
:match.SeizureFrequency1 = { rule = "hSeizure1b", CUI = :match.Lookup3.CUI, NoOfTimePeriods = :match.timePeriods.frequency, TimePeriod = :match.timePeriods.time-unit, QuantityTotal = :match.NumberRange.value}

/*
* Seizure in the last 3 months
* also picks up =  seizures have really increased since then, particularly in the last 2 years
*/


Rule: hSeizureFreq1c
Priority: 98
(
    ({ContextPrescription})?
    ({Lookup3.majorType == "Lookup", Lookup3.minorType == "Disease", !Lookup3.PREF=="Seizure free", Lookup3.Negation == Affirmed, Lookup3.Experiencer == Patient})
    ({NumberRange})
    ({timePeriods})
):match
-->
:match.SeizureFrequency1 = { rule = "hSeizure1c", CUI = :match.Lookup3.CUI, NoOfTimePeriods = :match.NumberRange.value, TimePeriod = :match.timePeriods.time-unit, QuantityTotal = "1"}

/*
* episode between 10 and 40 times a day, but also wrongly picking up "nocturnal events over the past 2 months"
*/


Rule: hSeizureFreq1d
Priority: 98
(
    ({ContextPrescription})?
    ({Lookup3.majorType == "epilepsy", Lookup3.minorType == "seizure", !Lookup3.PREF=="Seizure free", Lookup3.Negation == Affirmed, Lookup3.Experiencer == Patient})
    ({NumberRange})
    ({timePeriods})
):match
-->
:match.SeizureFrequency1 = { rule = "hSeizure1d", CUI = :match.Lookup3.CUI,
NoOfTimePeriods = :match.timePeriods.frequency, TimePeriod = :match.timePeriods.time-unit,
QuantityTotal = :match.NumberRange.value, QuantityLower = :match.NumberRange.N1, QuantityUpper = :match.NumberRange.N2}

/*
* SeizFreq = daily/weekly/monthly/per day/per week/per month
* used for one seizure per day this rule
*/

Rule: hSeizureFreq2a
Priority: 98
(
    ({ContextPrescription})?
    ({Lookup3.majorType == "Lookup", Lookup3.minorType == "Disease", Lookup3.Negation == Affirmed, Lookup3.Experiencer == Patient})
    ({SeizFreq})
):match
-->
:match.SeizureFrequency1 = { rule = "hSeizure2a", CUI = :match.Lookup3.CUI, NoOfTimePeriods = :match.SeizFreq.NoOfTimePeriods, TimePeriod = :match.SeizFreq.TimePeriod, QuantityTotal = :match.SeizFreq.QuantityTotal}

/*
* SeizFreq = daily/weekly/monthly/per day/per week/per month
* seizures three times per day
*/

Rule: hSeizureFreq2b
Priority: 99
(
    ({ContextPrescription})?
    ({Lookup3.majorType == "Lookup", Lookup3.minorType == "Disease", Lookup3.Negation == Affirmed, Lookup3.Experiencer == Patient})
    ({NumberRange})
    ({SeizFreq})
):match
-->
:match.SeizureFrequency1 = { rule = "hSeizure2b", CUI = :match.Lookup3.CUI, NoOfTimePeriods = :match.SeizFreq.NoOfTimePeriods, TimePeriod = :match.SeizFreq.TimePeriod, QuantityTotal = :match.NumberRange.value, QuantityLower = :match.NumberRange.N1, QuantityUpper = :match.NumberRange.N2}

/*
* SeizFreq = daily/weekly/monthly/per day/per week/per month
* used for one episode per day this rule
*/

Rule: hSeizureFreq2c
Priority: 99
(
    ({ContextPrescription})?
    ({Lookup3.majorType == "epilepsy", Lookup3.minorType == "seizure", Lookup3.Negation == Affirmed, Lookup3.Experiencer == Patient})
    ({NumberRange})
    ({SeizFreq})
):match
-->
:match.SeizureFrequency1 = { rule = "hSeizure2c", CUI = :match.Lookup3.CUI, NoOfTimePeriods = :match.SeizFreq.NoOfTimePeriods, TimePeriod = :match.SeizFreq.TimePeriod, QuantityTotal = :match.NumberRange.value, QuantityLower = :match.NumberRange.N1, QuantityUpper = :match.NumberRange.N2}
 
 
/*
*complex partial seizure. These happen most weeks and can be up to 3 times per day
*/

Rule: hSeizureFreq2d
Priority: 99
(
    ({ContextPrescription})?
    ({Lookup3.majorType == "Lookup", Lookup3.minorType == "Disease", Lookup3.Negation == Affirmed, Lookup3.Experiencer == Patient})
    ({Split})
    ({timePeriods})?
    ({NumberRange})
    ({SeizFreq})
):match
-->
:match.SeizureFrequency1 = { rule = "hSeizure2d", CUI = :match.Lookup3.CUI, NoOfTimePeriods = :match.SeizFreq.NoOfTimePeriods,
TimePeriod = :match.SeizFreq.TimePeriod, QuantityTotal = :match.NumberRange.value, QuantityLower = :match.NumberRange.N1,
QuantityUpper = :match.NumberRange.N2}



/*
* 3 seizures every 4 weeks
*/

Rule: hSeizureFreq3a
Priority: 100
(
    ({ContextPrescription})?
    ({NumberRange}):num1
    ({Lookup3.majorType == "Lookup", Lookup3.minorType == "Disease", Lookup3.Negation == Affirmed, Lookup3.Experiencer == Patient})
    ({NumberRange}):num2
    ({timePeriods})
):match
-->
:match.SeizureFrequency1 = { rule = "hSeizure3a2", CUI = :match.Lookup3.CUI, NoOfTimePeriods = :num2.NumberRange.value, TimePeriod = :match.timePeriods.time-unit, QuantityTotal = :num1.NumberRange.value}

/*
* smaller ones occur on a daily basis up to 10 times per day
*/

Rule: hSeizureFreq4a
Priority: 95
(
    ({ContextPrescription})?
    ({Lookup3.majorType == "epilepsy", Lookup3.minorType == "seizure", Lookup3.Negation == Affirmed, Lookup3.Experiencer == Patient})
    ({SeizFreq}):SF1
    ({NumberRange})
    ({SeizFreq}):SF2
):match
-->
:match.SeizureFrequency1 = { rule = "hSeizure4a2", CUI = :match.Lookup3.CUI, NoOfTimePeriods = :SF2.SeizFreq.NoOfTimePeriods, TimePeriod = :SF2.SeizFreq.TimePeriod, QuantityTotal = :match.NumberRange.value, QuantityLower = :match.NumberRange.N1, QuantityUpper = :match.NumberRange.N2}

/*
* once every few month she will have a slightly larger episode
*/

Rule: hSeizureFreq5a
Priority: 95
(
    ({ContextPrescription})?
    ({NumberRange}):n1
    ({NumberRange}):n2
    ({timePeriods})
    ({Lookup3.majorType == "epilepsy", Lookup3.minorType == "seizure", Lookup3.Negation == Affirmed, Lookup3.Experiencer == Patient})
):match
-->
:match.SeizureFrequency1 = { rule = "hSeizure5a2", CUI = :match.Lookup3.CUI, NoOfTimePeriods = :n2.NumberRange.value, TimePeriod = :match.timePeriods.time-unit, QuantityTotal = :n1.NumberRange.value, QuantityLower = :n1.NumberRange.N1, QuantityUpper = :n1.NumberRange.N2}


